
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://libraries.minecraft.net/'
    }

    mavenLocal()
}

configurations {
    shade
    compile.extendsFrom(shade)
}

dependencies {

    compileOnly("org.spigotmc:spigot:${project.ext.minecraftVersion}-R0.1-SNAPSHOT")
    //compileOnly("org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT")

    shade(project(':api'))
    shade(project(':common'))

}

processResources {

    Map<String, ?> ex = new HashMap<>()
    ex.put("version", project.ext.fullVersion)
    ex.put("modid", project.ext.modid)
    ex.put("modname", project.ext.modname)
    ex.put("modclass", project.ext.modclass)
    ex.put("minecraftVersion", project.ext.minecraftMajorVersion)

    filesMatching("plugin.yml") {
        expand(ex)
    }
}

shadowJar {

    setArchivesBaseName("${project.ext.archiveName}-spigot-mc${project.ext.minecraftVersion}")
    configurations = [ project.getConfigurations().shade ]

    dependencies {

        include(project(':api'))
        include(project(':common'))

    }

}

artifacts {
    jar
    sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = project.ext.publicationVersion
            artifact(jar)
            artifact(sourcesJar)
        }
    }
    repositories {
        maven {
            if (project.hasProperty("pubUrl")) {
                url = project.pubUrl as String
                credentials {
                    username project.pubUser as String
                    password project.pubPass as String
                }
            }
        }
    }
}